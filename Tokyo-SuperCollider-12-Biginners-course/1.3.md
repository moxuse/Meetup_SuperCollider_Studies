# SC入門講座

>Tokyo SuperCollider#26 @Upgrade English 2017/05/28
>Tokyo SuperCollider#12 @Upgrade English 2016/01/31

### 1.3


## エンベロープ


サイン波にエンべロープをかけみます。

`EnvGen` というUGenを使います。

```
x = {SinOsc.ar(440, 0, 0.2) * EnvGen.ar(Env.linen)}.play;
```

`Env` といのはエンペローブを整形してくれるクラスです。
`.linen` は台形のエンペローブです。

ローカルサーバウィンドウの Synths を見てください。1のままです。音は出ていませんがシンセが生成されたまままだ処理が続いています。

音を止めましょう。

```
x.free;
```

エンベロープが終わったタイミングでシンセを消去したいので、
`doneAction` という引数を `2` で埋めます。

```
x = {SinOsc.ar(440, 0, 0.2) * EnvGen.ar(Env.linen, doneAction:2)}.play;
```

エンベロープが終わるとシンセが消去されます。



`.test` でサイン波で確かめる事ができます。さらに.plotでグラフを表示できます。

```
Env.linen(1, 2, 3, 0.6).test.plot;
```

その他の
### Envメソッド
- `Env.new([0, 1, 0.3, 0.8, 0], [2, 3, 1, 4], -2).test.plot;`
- `Env.triangle(1, 1).test.plot;`
- `Env.sine(1,1).test.plot;`
- `Env.perc(0.05, 1, 1, -4).test.plot;`
- `Env.asr(0.02, 0.5, 1, 1, -4).test.plot;`
- `Env.adsr(0.02, 0.2, 0.25, 1, 1, -4).test.plot;`
- `Env.dadsr(0.5,0.02, 0.2, 0.25, 1, 1, -4).test.plot;`
- `Env.cutoff(1, 1, 4).test.plot;`



### Decay UGen

トリガーを使ったエンべロープ

```
(
{ var trig;
	trig = Impulse.kr(2);
	SinOsc.ar(400, 0, 0.3) * Decay.kr(trig, 0.2)}.play;
)
```

```
(
{ var trig;
	trig = Impulse.kr(1);
	SinOsc.ar(400, 0, 0.3) * Decay2.kr(trig, 0.001, 1)}.play;
)
```

## ポリフォニー

### マルチチャンネル


SuperColliderではオーディオシグナルのチャンネルを`Bus`(バス)と呼ばれるは0 ~ 127までの整数で管理しています。
`Bus`にはコントロールバス、オーディオバス、インプット／アウトプットバスなどあります。がここではオーディオバスについて触れます。


基本的に1チャンネルごとに1プロセスをあたえなくてはいけません。

```
{SinOsc.ar(440, 0, 0.3)}.play;
```

Lチャンネルしか鳴りません。


```
{SinOsc.ar(440, 0, 0.3).dup}.play;
```

LR同じシグナルが鳴ります。`.dup`はオブジェクトをコピーしてくれます。



### List

UGenの中ではListはそれぞれ違ったチャンネルに割り当てられます。このときUGenのインスタンスは複数作られます。

```
{Pulse.ar([400, 500, 600],[0.5, 0.1], 0.2)}.play
```

上のプログラムは以下と同じ。

```
{[Pulse.ar(400, 0.5, 0.2), Pulse.ar(500, 0.1, 0.2), Pulse.ar(600, 0.5, 0.2) ]}.play;
```

### Mix

```
{Mix.new(Pulse.ar([400, 501, 600], [0.5, 0.1], 0.1))}.play;
```

Mixは複数のシグナルをミックスして一つのチャンネルに束ねます。束はListで表現します。

```
{Mix.fill(12 ,{SinOsc.ar({ 3000.rand }, 0, 0.05)})}.play;
```

Mix.fillはnに指定した数だけfunctionを繰り返し一つのチャンネルにまとめます。


```
Mix.fill(n, function);
```


## シーケンスパターンを作る

さて、シンセが作れたところでシーケンスを組み音楽へと発展させていきたいと思うでしょう。いくつかの方法がありますが、ここでは`Pattern`と呼ばれるシーケンスパターンを作るクラス群を用いて音のシーケンスを作ってみます。


### Pbind

変数に代入する方法。

```
(
a = Pbind(\dur, 0.5).play;

b = Pbind(\dur, 0.5, \note, Pseq([0, 1, 2, 3], 10)).play;

c = Pbind(\dur, 0.25, \note, Pseq([0, 1, 2, 3, 4, 5, 6, 7, 8, 9], inf), \amp, Prand([0, 0.3], inf)).play;
)
```

`.play()` メソッドで実行。

```
Pbind(\dur,0.5).play;

Pbind(\dur,0.5,\note,Pseq([0,1,2,3],10)).play;

Pbind(\dur,0.25,\note,Pseq([0,1,2,3,4,5,6,7,8,9],10),\amp,Prand([0,0.3],inf)).play;
```

自分で作ったシンセでならす。

```
SynthDef("mySynth", {arg freq, amp;
	var out;
 	out = Pulse.ar(freq, 0.1, amp).dup * EnvGen.ar(Env.perc, doneAction:2); //エンべロープをつけて音を減衰させる。
 	Out.ar(0, out);
}).store;
```

```
Pbind(\instrument, "mySynth", \dur, 0.25, \note, Pseq([0, 1, 2, 3, 4, 5, 6, 7, 8, 9], inf), \amp, Prand([0, 0.3],inf)).play;
```


### Ppar

複数のストリームを束ねます。

```
(
a = Ppar(
	[
		Pbind(\dur,1),

		Pbind(\dur,0.75,\note,Pseq([0,1,2,3],inf)),

		Pbind(\dur,0.25,\note,Pseq([0,1,2,3,4,5,6,7,8,9],inf),\amp,Prand([0,0.3],inf))
	]).play;
)


a.stop;
```




### Pdef

ストリームを名前で管理。

```
(
Pdef(\test_pdef,
	Ppar(
		[
			Pbind(\dur, 1, \note,Pseq([5, 0, 2], inf)),
	
			Pbind(\dur, 0.75, \note, Prand([0, 2, 5, 7, 9, 12] + 12, inf)),
	
			Pbind(\dur, 0.125, \note, Prand([0, 2, 5, 7, 9, 12] - 12, inf),\amp, Prand([0, 0.3], inf))
		])
	)
)
Pdef(\test_pdef).play;
Pdef(\test_pdef).stop;
```



***＊自分のシンセを作ってストリームで演奏してみましょう***


## サウンドファイルを扱う


サウンドファイルを扱うには `Buffer` を使います。
サウンドファイルはsounds/以下におくのが良いでしょう。

```
b = Buffer.new(s); // サーバにバッファを作ります
b.allocRead("sounds/a11wlk01.wav"); // サウンドファイルを割り当てます。

x = {PlayBuf.ar(1, b.bufnum, BufRateScale.kr(b.bufnum), loop: 1) * 0.5}.play;
x.free; b.free;
```






### TGrains

グラニューラシンセシスのためのUGen


```
s.sendMsg(\b_allocRead, 10, "sounds/a11wlk01.wav");
```


```
(
{
	var b = 10, trate, dur, rate;
	trate = MouseY.kr(2,200,1);
	dur = 4 / trate;
	rate = 1;
	TGrains.ar(2, Impulse.ar(trate), b, rate, MouseX.kr(0,BufDur.kr(b)), dur, Dseq([-1, 1], inf), 0.1, 2);
}.play;
)
```


### メモ（2016/03/27）

```
/**
 * SynthDefのメソッドの違い  
 */

(
SynthDef("test", {arg freq = 440;
  Out.ar(0, SinOsc.ar(freq, 0, 0.3)); 
}).send(s);
)

OSCdef({|msg, time, addr, recvPort | msg.postln}, '/var', n); // <- wrang code

(
SynthDef("test", {arg freq = 440;
  Out.ar(0, SinOsc.ar(freq, 0, 0.3)); 
}).writeDefFile();
)

s.sendMsg("/s_new", "test", 1000, 0, 0);

Synth("test", 1000);

(
SynthDef("test", {arg freq = 440;
  Out.ar(0, SinOsc.ar(freq, 0, 0.3)); 
}).store()
)

/**
 * スケールの変化
 */
 
a = Scale.major;
a.degrees;        // [ 0, 2, 4, 5, 7, 9, 11 ]
a.semitones;        // [ 0, 2, 4, 5, 7, 9, 11 ]
a.cents;        // [ 0, 200, 300, 500, 700, 900, 1100 ]
a.ratios;        // [ 1, 1.1224620483089, 1.2599210498937, 1.3348398541685, etc. ]

Pbind(\scale, a, \degree, Pseq((0..7) ++ (6..0) ++ [\rest], 1), \dur, 0.25).play;

// use non-standard tuning
a.tuning_(\minor);

/**
 * トーンシンセとエフェクターのスケジュール
 */
 
(
Ppar(
  [
    Pbind(\instrument, "pulse", \dur, 2, \midinote, Pseq([0, 5, 7] + 35,inf)),   
  ]
).play()
)

(
s.sendMsg("/s_new", \effect, 2000, 1, 1);
t = Task({
  inf.do({
  var fx;
      fx = Pfunc({
        var pat;
        pat = [400,300,700, 12000].choose;
        s.sendMsg("/n_set", 2000, \freq, pat);
      postln("send:")
      }).asStream;
  fx.next;
  0.5.wait;
  })
}).play()
)

t.stop();

```

### メモ（2017/05/28）

```

// s = Server.local; //自分のマシンのサーバ
s.boot; //ブート

s.quit; //サーバーを終了

thisProcess.hardStop(); // すべてのプロセス終了

s.freeAll(); // すべてのノード開放

s.options.device = "Built-in Microphone"; // シンセの名前を指定しその後にブート。
s.boot;

{SinOsc.ar(440, 0, 0.3)}.play();


(
SynthDef("mySynth", {
  arg freq=440, amp=0.3, my_width=0.5;
  var out;
  out = LFPulse.ar(freq, 0, my_width, amp) * EnvGen.ar(Env.linen, doneAction:2);
  out = Pan2.ar(out, 1);
  Out.ar(0, out);
}).store;
)

(0..15)

s.sendMsg("/s_new", "mySynth", 4000, 0, 1); // グループ1のID 1004にhelp-sineを生成

s.sendMsg("/n_set", 4000, "freq", 6000, "my_width", 0.02);


s.sendMsg("/n_free", 4000);

s.queryAllNodes();



x = {SinOsc.ar(440, 0, 0.3) * EnvGen.ar(Env.linen)}.play;


Env.new([0, 1, 0.3, 0.8, 0], [2, 3, 1, 4], -2).test.plot


Env.triangle(1, 1).test.plot;

Env.adsr(0.02, 0.2, 0.25, 1, 1, -4).test.plot;

x.free;

s.freeAll();

x = {SinOsc.ar(440, 0, 0.2) * EnvGen.ar(Env.linen, doneAction:2)}.play;

```

